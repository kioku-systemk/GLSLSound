<html>
    <head>
        <script src="glslsound.js"></script>
        <script>
        var soundShader = (function () {/*
   #define TAU 6.283185307179586476925286766559
#define PI 3.1415926535897932384626433832795

#define C  32.703
#define CS 34.648
#define D  36.708
#define DS 38.891
#define E  41.203
#define F  43.654
#define FS 46.249
#define G  48.999
#define GS 51.913
#define A  55.0
#define AS 58.270
#define B  61.735

#define BPM 128.0
#define SEC_PER_MIN 60.0

//-------------------------------------------------------------------
//---------------------------Instruments-----------------------------
//-------------------------------------------------------------------

float noise( float x ){return fract(sin(1371.1*x)*43758.5453);}

//Basic waveforms with note and octave input
float sn(float t, float note, float octave)
{
	return sin(t*note*exp2(octave)*PI);
}

float saw(float t, float note, float octave)
{
	return fract(t*note*exp2(octave-1.))-0.5;
}

float tri(float t, float note, float octave)
{
    //return abs(4.0 * mod(freq * time - 0.25, 1.0) - 2.0) - 1.0;
	return (abs(fract(t*note*exp2(octave-1.))-0.5)*2.-0.5)*2.;
}

float sqr(float t, float note, float octave)
{
	return step(fract(t*note*exp2(octave-1.)), 0.5)-0.5;
}

//-------------------------------------------------------------------
//----------------------------Mixing---------------------------------
//-------------------------------------------------------------------

//Simple mixing function with balance control  (balance range 0..1)
vec2 mixb(float x, float bal)
{
	bal = clamp(bal,0.,1.);
	return vec2(x * bal, x*(1.-bal));
}

// cheap and unrealistic distortion
float dist(float s, float d)
{
	return clamp(s * d, -1.0, 1.0);
}
vec2 dist(vec2 s, float d)
{
	return clamp(s * d, -1.0, 1.0);
}

// quantize
float quan(float s, float c)
{
	return floor(s / c) * c;
}

// randomize
float nse(float x)
{
	return fract(sin(x * 110.082) * 19871.8972);
	//return fract(sin(x * 110.082) * 13485.8372);
}
float nse_slide(float x)
{
	float fl = floor(x);
	return mix(nse(fl), nse(fl + 1.0), smoothstep(0.0, 1.0, fract(x)));
}

float kick(float tb)
{
	float aa = 15.0;
	tb = sqrt(tb * aa) / aa;
	
	float amp = exp(max(tb - 0.1, 0.0) * -10.0);
	float v = sin(tb * 120.0 * TAU) * amp;
	v = dist(v, 1.0) * amp;
	//v += nse(quan(tb, 0.001)) * nse(quan(tb, 0.00001)) * exp(tb * -20.0) * 2.5;
	return v;
}

float hat(float tb)
{    
	float aa = 4.0;
	tb = sqrt(tb * aa) / aa;
	float v = nse(sin(tb * 4000.0) * 0.0001) 
        * smoothstep(0.0, 0.02, tb) 
        * smoothstep(0.02, 0.0, tb - 0.05);//exp(tb * -10.0);
    
	float amp = exp(max(tb, 0.0) * -20.0);
	v = dist(v, 1.0) * amp;
    
    return v;
}

float snare(float tb)
{
	float aa = 1.75;
	tb = sqrt(tb * aa) / aa;
    
    float v = tri(tb, A, 2.0);
    float env = exp(max(tb, 0.0) * -30.0);
    float attack = v * env;
    
	float v2 = nse(sin(tb * 4000.0) * 0.0001) 
        * smoothstep(0.0, 0.15, tb)
        * (1.0-smoothstep(0.23, 0.25, tb));
    float env2 = exp(max(tb - 0.05, 0.0) * -13.0);
    float decay = v2 * env2;
    
    attack = dist(attack, 3.5);
    decay += dist(attack, 2.5);
    return attack + decay;
}

vec2 organ(float tb, float note, float octave)
{
    vec2 v = vec2(0);
    float env = 0.75;// exp(max(tb, 0.0) * -0.1);
    v += 0.33*sn(tb, note, octave) * env;
    v += 0.33*dist(sqr(tb + 0.0002 * cos(8.0*tb), note, octave + 1.0), 4.0) * env;
    v += 0.33*dist(saw(tb + 0.0002 * sin(8.0*tb), note, octave + 2.0), 2.0) * env;
    v += 0.05*nse(sin(tb * 4000.0) * 0.0001) * env;
    return v;
}

#define CHORD3(nl, n1, n2, n3) if(floor(beat) >= bc && floor(beat) - bc < nl){mx += 0.33 * sc * organ(tb / 16.0, n1, 3.0);        mx += 0.33 * sc * organ(tb / 16.0, n2, 3.0);        mx += 0.33 * sc * organ(tb / 16.0, n3, 3.0);    }    bc += nl;
#define CHORD4(nl, n1, n2, n3, n4) if(floor(beat) >= bc && floor(beat) - bc < nl){mx += 0.33 * sc * organ(tb / 16.0, n1, 3.0);        mx += 0.33 * sc * organ(tb / 16.0, n2, 3.0);        mx += 0.33 * sc * organ(tb / 16.0, n3, 3.0);       mx += 0.33 * sc * organ(tb / 16.0, n4, 2.0);    }    bc += nl;
                                  
vec2 mainSound(float time)
{
    float t = time;
	float tb = mod(time * 4.0 * BPM / SEC_PER_MIN, 16.0); //fraction of beat?
    vec2 mx = vec2(0);
    
    //mx += hat(fract((tb) / 2.0) * 0.25) * 0.25;
    float drum_vol = 0.5;
    mx += 1.0 * kick(fract(tb / 4.0) * 0.5);
    mx += 1.0 * mixb(hat(fract((tb + 0.002 + 2.0) / 4.0) * 0.5), -1.0);
    mx += 1.0 * mixb(hat(fract((tb + 2.0) / 4.0) * 0.5), 1.0);
    mx += 1.0 * mixb(snare(fract((tb + 4.0) / 8.0) * 0.5), -1.0);
    mx += 1.0 * mixb(snare(fract((tb + 0.002 + 4.0) / 8.0) * 0.5), 1.0);
    mx *= drum_vol;
    
	float sc = 0.75 + 0.25 * smoothstep(0.25, 0.5, fract(tb / 8.0) );//sidechain every 8th
    //mx += 0.25 * organ(fract((tb + 2.0) / 4.0) * 0.5, D, 2.0) * sc;
    
    //F#, C#, A# 4 beats
    //D#, C, G# 1.5 beats
    //F#, C#, A#, F# 2 beats
    //D#, A#, G, D# 8.5 beats
    
    //F#, C#, A# 4 beats
    //D#, C, G# 1.5 beats
    //F#, C#, A#, F# 2 beats
    //D#, A#, G, D# 7 beats
    //D#, G#, C, G# 1.5 beats
    
	float beat = mod(time * BPM / SEC_PER_MIN, 32.0); // beat counter
    float bc = 0.0;
    
    CHORD3(4.0, F, CS, AS)
    CHORD3(1.5, DS, C, GS)
    CHORD4(2.0, FS, CS, AS, FS)
    CHORD4(8.5, DS, AS, G, DS)
        
    CHORD3(4.0, F, CS, AS)
    CHORD3(1.5, DS, C, GS)
    CHORD4(2.0, FS, CS, AS, FS)
    CHORD4(7.0, DS, AS, G, DS)
        
    CHORD4(1.5, DS, C, GS, DS)
    
    return vec2(mx);
}
        */}).toString().match(/\n([\s\S]*)\n/)[1];

        window.onload = function () {
            sound = new GLSLSound();
            sound.setGain(0.5);
            var cr = sound.compleShader(soundShader);
            if (!cr) {
                var msg = document.createTextNode('Compile failed:' + sound.getError());
                document.body.appendChild(msg);
            } else { 
                sound.prepare(5.0); // 5.0[sec] sound
                
                var button = document.createElement('button');
                document.body.appendChild(button);
                button.innerHTML = "play GLSL";
                button.addEventListener('click', function () {
                    sound.play();
                });
            }
        }
        </script>
    </head>
<body>
</body>
</html>