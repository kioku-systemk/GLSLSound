<html>
    <head>
        <script src="glslsound.js"></script>
        <script>
        var soundShader = (function () {/*
  const float BPM = 140.;
const float QL = 60. / (BPM * 4.);
const float END = 60. / QL;
float hash(float x){return fract(sin(x)*265871.1723);}

const float pi2 = 3.1415926 * 2.;
float p212 = pow(2., 1./12.);
float ntof(float n){return 440.*pow(p212,n-48.);}

float osc_noise(float p) {
    p *= 20000.;
    float F = floor(p), f = fract(p);
    return mix(hash(F), hash(F+1.), f);
}
float osc_sine(float p){return sin(p*pi2);}
float osc_saw(float p){return p*2.-1.;}
float osc_square(float p,float x){return -1.+2.*step(x,p);}
float osc_sinefm(float pc, float pm, float b){return sin(pc*pi2+b*sin(pm*pi2));}
float osc_tri(float p, float x) {
    float a = smoothstep(0., x, p), b = smoothstep(1., x, p);
    return (a * b) * 2. - 1.;
}

float phase_freq(float t, float f){return fract(f*t);}

float env_ar(float t, float a, float r){return smoothstep(0.,a,t)*smoothstep(r,0.,t-a);}
float env_asr(float t, float ts, float a, float r) {
    return smoothstep(0.,a,t)*smoothstep(r,0.,t-a-ts);
}


float kick(float t) {
    float S = 0.;
    {
    	float kf = smoothstep(.13, .0, t);kf *= kf * kf;
    	float s = osc_sine(phase_freq(t, (30. + kf * 200.)));
    	float ka = smoothstep(.3, .0, t);
    	float a = ka;//(ka * ka);
        S += s * a;
    }
    {
    	float s = osc_sine(phase_freq(t,ntof(12.)));
    	float ka = smoothstep(.3, .0, t);
    	float a = ka;//(ka * ka);
        S += s * a * .3;
    }
    {
    	float kf = smoothstep(.015, .0, t);kf *= kf * kf;
    	float s = osc_noise(t*phase_freq(t, (40. + kf * 200.)));
    	float ka = smoothstep(.02, .0, t);
    	float a = .3 * (ka * ka);
        S += s * a;
    }
    return S;
}

float hh(float gt, float t) {
    float S = 0.;
    {
    	float s = osc_square(phase_freq(t,sin(gt) + ntof(36.)),.5);
    	float ka = smoothstep(.23, .0, t);
    	float a = (ka * ka);
        S += s * a * .5;
    }
    {
    	float s = osc_noise(t*(.1 + hash(floor(gt*8.))));
    	float ka = smoothstep(.06, .0, t);
    	float a = (ka * ka);
        S += s * a;
    }
    return 1.32 * pow(S, 3.);
}

float snare(float gt, float t) {
    float S = 0.;
    {
    	float s = osc_saw(phase_freq(t,sin(gt) + ntof(23.)));
    	float ka = smoothstep(.3, .0, t);
    	float a = (ka * ka);
        S += s * a * .2;
    }
    {
    	float s = osc_noise(t*(.1 + hash(floor(gt*3.))));
    	float ka = smoothstep(.3, .0, t);
    	float a = (ka * ka);
        S += s * a;
    }
    return 4. * pow(S, 3.);
}

#define PTDECL(l) const float PT=QL*l;float pn=floor(t/PT),pt=mod(t,PT),s=0.;
#define QTT(n) (float(n)*QL)

float note1(float gt, float t, float PT, float b, float n) {
    t -= b;
    if (t < 0.) t += PT;
    float p = phase_freq(t,ntof(n));
    float s = 0.;
	//s += (.03+.02*sin(gt*1.7)) * osc_square(p, .5+.4*sin(gt));
	s += .3 * osc_sine(p);
    s += .503 * osc_tri(p, .5+.3*sin(gt));
    return s * env_ar(t,.001,1.);
}

float ptr0(float t) {
    PTDECL(32.);
#define NOTE(p,n) s+=note1(t,pt,PT,QTT(p),n+12.);
    NOTE(0, 48.);
    NOTE(6, 43.);
    NOTE(8, 46.);
    NOTE(12, 41.);
    NOTE(15, 46.);
#undef NOTE
    return s;
}


float ptr1(float t) {
    PTDECL(32.);
#define NOTE(p,n) s+=note1(t,pt,PT,QTT(p),n+12.);
    NOTE(0, 48.);
    NOTE(6, 43.);
    NOTE(8, 46.);
    NOTE(12, 41.);
    NOTE(15, 46.);
    NOTE(16, 48.);
    NOTE(22, 43.);
    NOTE(24, 46.);
    NOTE(28, 41.);
    NOTE(30, 46.);
#undef NOTE
    return s;
}

float note2(float gt, float t, float PT, float b, float n) {
    t -= b;
    if (t < 0.) t += PT;
    float p = phase_freq(t,ntof(n));
    float s = 0.;
	//s += .3 * osc_sine(p);
    s += .3 * osc_sinefm(phase_freq(t,ntof(n-12.)), p, 3.+sin(gt*4.));
    s += .4 * osc_tri(p, .15);//.5 + .4 * sin(gt*.9));
    return .7*s * env_ar(t,.04,2.);
}

float pat_v2(float t) {
    PTDECL(64.);
#define NOTE(p,n) s+=note2(t,pt,PT,QTT(p*2),n);
    NOTE(0, 48.);
    NOTE(8, 46.);
    NOTE(15, 51.);
    NOTE(22, 43.);
    NOTE(28, 41.);
#undef NOTE
    return s;

}

float pat_snare0(float t) {
    PTDECL(32.);
#define SN(p) {float tt=pt-QTT(p);if(tt>=0.)s+=snare(t,tt);}
    SN(8);
    SN(24);
#undef SN    
    return s;
}

#define HH(p) {float tt=pt-QTT(p);if(tt>=0.)s+=hh(t,tt);}
float pat_hh0(float t){PTDECL(2.);HH(0);return s;}
float pat_hh1(float t) {
    PTDECL(32.); HH(11); HH(15); HH(25); HH(29);
    return s;
}
#undef HH

#define KICK(p) {float tt=pt-QTT(p);if(tt>=0.)s+=kick(tt);}
float pat_kick0(float t) {
    PTDECL(32.);
    KICK(0);
    KICK(6);
    KICK(13);
    KICK(16);
    KICK(22);
    return s;
}

float pat_kick1(float t) {
    PTDECL(32.);
    KICK(27);
    KICK(30);
    return s;
}
#undef KICK

float busn(float gt, float t, float PT, float b, float e, float n) {
    t -= b; e -= b;
    if (t < 0.) {t += PT;}
    float p = phase_freq(t,ntof(n));
    float s = 0.;
    s += osc_tri(p, .5 + .4 * sin(gt*3.));
    s += 2.*osc_tri(phase_freq(t,ntof(n-12.)), .2 + .1 * sin(gt*2.3));
    s += 2.*osc_sine(phase_freq(t,ntof(n-12.)));
    s += osc_sinefm(p, phase_freq(t+313.,ntof(n-12.)), 5.+3.3*sin(gt*4.));
    return s * env_asr(t,e,.01,.1);
}


float bus(float t) {
	PTDECL(64.)
#define NOTE(p,pe,n) s+=busn(t,pt,PT,QTT(p),QTT(pe),n-12.);
    NOTE( 0, 16, 32.);
    NOTE(16, 32, 34.);
    NOTE(32, 64, 29.);
#undef NOTE
    return s;
}

vec2 pptr0(float t) {
  	float doff = QL*12., dk = .5;
  	return .4 *
      	( vec2(ptr0(t),        ptr0(t+.001*sin(1.3*t)))
       	+ vec2(ptr0(t-doff),   ptr0(t+.001*sin(2.1*(t-doff)))) * dk
       	+ vec2(ptr0(t-doff*2.),ptr0(t+.003*sin(3.7*(t-doff*2.)))).yx * dk * dk
    );
}

vec2 pptr1(float t) {
  	float doff = QL*12., dk = .5;
  	return .4 *
      	( vec2(ptr1(t),        ptr1(t+.001*sin(1.3*t)))
       	+ vec2(ptr1(t-doff),   ptr1(t+.001*sin(2.1*(t-doff)))) * dk
       	+ vec2(ptr1(t-doff*2.),ptr1(t+.003*sin(3.7*(t-doff*2.)))).yx * dk * dk
    );
}

vec2 synth(float t) {
    float line = floor(t / QL);
    vec2 s = vec2(0.);
    float tt = t;
    
#define LN(a,b) (b > line && line >= a)
    if LN(0.,16.) {
        s += pptr0(t);
    }
    if (LN(16.,320.) || LN(448.,560.)) {
        s += pptr1(t);
    }
        
    if LN(432.,448.) {
        float dt = t - QL*432.;
        tt = QL*448. - dt;
        s += smoothstep(0.,QL*16.,dt) * pptr1(t);
    }
    
    if LN(320.,560.) {
        s += .8*vec2(pat_v2(tt),pat_v2(tt+.003));
    }

    if LN(64.,560.) {
        s += .23 * vec2(pat_hh0(t-.002*sin(t*1.11)),pat_hh0(t));
        s += .23 * vec2(pat_hh1(t-.002*sin(2.+t*1.11)),pat_hh1(t));
    	s += .23 * vec2(pat_snare0(t-.002*sin(3.+t*1.11)),pat_snare0(t));
    	s += 1.4 * vec2(pat_kick0(t-.002*sin(t*1.11)),pat_kick0(t));
    }
    
    if LN(160.,560.) {
        s += 1.4 * vec2(pat_kick1(t-.002*sin(t*1.11)),pat_kick1(t));
    }
    
    if LN(192.,560.) {
    	s += .27 * vec2(bus(t));
    }

    return s;
}

vec2 mainSound(float time) {
    float line = floor(time / QL);
    float a = 1.;
    
#if 0
    const float LA = 304., LB = 368.;
    time = (LA + mod(time / QL, LB-LA)) * QL;
    line = floor(time / QL);
#endif

    
    if LN(316.,320.) {
        float t = time - QL*316.;
        time = QL*316. - t;
    }
    
    if LN(512.,516.) {
        float t = time - QL * 512.;
      	time = QL * 512. + mod(t,QL*.5);
    }
    
    if LN(524.,532.) {
        float t = time - QL * 524.;
      	time = QL * 524. - t;
    }
    
    if LN(536.,540.) {
        float t = time - QL * 536.;
      	time = QL * 536. - mod(t,QL*.5);
    }
    
    if (line > 544.) {
        float dl = 560. - 544.;
        float t = time - QL*544.;
        float T = dl * QL;
        time = QL * 544. + t * (1. - t / (2. * T));
        
        a = smoothstep(T, 0., t);
    }
    
    vec2 s = a * synth(time);
    return .39 * s;
}
        */}).toString().match(/\n([\s\S]*)\n/)[1];

        window.onload = function () {
            sound = new GLSLSound();
            sound.setGain(0.5);
            var cr = sound.compleShader(soundShader);
            if (!cr) {
                var msg = document.createTextNode('Compile failed:' + sound.getError());
                document.body.appendChild(msg);
            } else { 
                sound.prepare(5.0); // 5.0[sec] sound
                
                var button = document.createElement('button');
                document.body.appendChild(button);
                button.innerHTML = "play GLSL";
                button.addEventListener('click', function () {
                    sound.play();
                });
            }
        }
        </script>
    </head>
<body>
</body>
</html>